#ifndef __CDRAG__
#define __CDRAG__

#include <InterpIncludes.ncp>

class CustomerDrag extends Element {

//------------------------------------------------------------
//     ******* DOCUMENTATION *******
//------------------------------------------------------------

  title ="";
  description = isA() + " calculates Customer drag per ARP5571 requirements";
  usageNotes ="";
  background = "";

//------------------------------------------------------------
//  SETUP VARIABLES
//------------------------------------------------------------
  real Fd_in {
    value = 0.0; units = "lbf"; IOstatus = "input";
    description = "User input drag";
  }
  real Fd_dmd {
    value = 0.0; units = "lbf"; IOstatus = "output";
    description = "Demand drag";
  }
  real Fd {
    value=0.0;  units="lbf";  IOstatus="output";
    description="Drag";
  }

  // this is to match the ARP5571
  Option switchFd {
    allowedValues = { "OFF=0", "INPUT=1", "CALCULATE=99", "CUSTOM=100" }
    description="Drag method demand switch
 |-----------------|-----------------|----------------------------------------|
 | switchFd        | Input Parameter |               Description              |
 |-----------------|-----------------|----------------------------------------|
 | OFF or 0        |                 | Drag not calculated, off               |
 |-----------------|-----------------|----------------------------------------|
 | INPUT or 1      | Fd_in           | Drag is input                          |
 |-----------------|-----------------|----------------------------------------|
 | CALCULATE or 99 |                 | Supplier supplied drag calculation     |
 |-----------------|-----------------|----------------------------------------|
 | CUSTOM or 100   |                 | Customer supplied drag calculation     |
 |-----------------|-----------------|----------------------------------------|
";
    trigger = FALSE;
  }



//------------------------------------------------------------
//  SOCKETS
//------------------------------------------------------------

  
  Socket S_customFd {
    allowedValues = { "Fd_dmd" }
    description = "Drag customer socket";
    socketType = "CUSTOM_HOOK";
  }
 

//----------------------------------------------------------
// VARIABLE CHANGED METHODOLOGY
//------------------------------------------------------------
  void variableChanged( string name, any oldVal ) {
    variableChangedTBName(name, oldVal);
  }

//------------------------------------------------------------
//  PERFORM ENGINEERING CALCULATIONS
//------------------------------------------------------------

  void calculate() {
    if(      switchFd == "OFF"     )   { Fd_dmd = 0.; }
    else if( switchFd == "INPUT"     ) { Fd_dmd = Fd_in; }
    else if( switchFd == "CALCULATE" ) { Fd_dmd = calcCALCULATE(); }
    else if( switchFd == "CUSTOM" ) {
      Fd_dmd = 0.;
      if( !S_customFd.isEmpty() ) { S_customFd.execute(); } //returns Fd_dmd
    }
    Fd = Fd_dmd;
  }

}
#endif