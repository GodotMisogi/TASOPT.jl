#ifndef THERMO
	#define THERMO GasTbl
#endif

setThermoPackage("$THERMO");

Element Assembly Core {
	
	// Assembly level elements for solution control, interfacing, and power setting
	Option switchDes {
		description = "Determines if the engine core assembly is in design or off-design mode";
		IOstatus = "input";
		rewritableValues = FALSE;
		trigger = TRUE;
		allowedValues = {"DESIGN","OFFDESIGN"};
	}
    // ------------------------------
    // Compressor CmpH
    // ------------------------------
    Element Compressor CmpH
    {
		// PRdes    = 11.1;//9.02;
		// effDes   = 0.87;
		// Sh_O.inertia   = 10.;     // slugs-ft**2.
		// #include "hpcE3.map"
		
		PRdes    = 9.95;//10.894;
		effDes   = 0.88;
		NcDes    = 7000;
		Sh_O.inertia   = 10.;     // slugs-ft**2.
		#include "hpcCFM56.map"
		
		InterStageBleedOutPort  Fl_B_CH
		{
			//switchFlow = "ABSOLUTE"
			fracBldWork	= 0.5;
			fracBldP = 0.5;
			fracBldW = 0.023;
		}		
		InterStageBleedOutPort  Fl_B_NC
		{
			//switchFlow = "ABSOLUTE";
			fracBldWork	= 0.65;
			fracBldP = 0.3;
			fracBldW = 0.048;
		}	
		
		// Customer Interstage HPC bleed
		InterStageBleedOutPort  Fl_B027
		{
			switchFlow = "ABSOLUTE";
			fracBldWork	= 0.65;
			Wbld = 0.;
		}
		Fl_O.MNdes = 0.25;
    }

    // ------------------------------
    // Bleed B030
    // ------------------------------
    Element Bleed B030
    {
		//Fl_O.MN = 0.4; 

		// Turbine Cooling and Leakage Air (TCLA) stream 1 
		// conisdered NON CHARGEABLE (connects to turbine inlet)
		BleedOutPort TCLA_NC {	              
			fracW = 0.100;  //fracW = Ratio of bleed flow to Bleed element inlet flow
		}	
		// Turbine Cooling and Leakage Air (TCLA) stream 2 
		//considered CHARGEABLE (connects to turbine exit)
		BleedOutPort TCLA_CH {	              
			fracW = 0.100; //fracW = Ratio of bleed flow to Bleed element inlet flow
		} 
		BleedOutPort Fl_B030B
        {
            hscale = 1.0;
            fracW = 0.0;
        }
		Fl_O.MNdes = 0.3;
    }
    // ------------------------------
    // Duct Diffuser
    // ------------------------------
    Element Duct D031
    {
		// switchDP = "INPUT";

        // void preexecute()
        // {
            // dPqP_in = 0.15 * Fl_I.MN * Fl_I.MN;
        // }

        Fl_O.MNdes = 0.3;
    }
    // ------------------------------
    // FuelStart FusPri
    // ------------------------------
    Element FuelStart FusPri
    {
        // Tref =  518.67;
        //hRef = 184.294;
		// Tref = 518.67;
        Wfuel = 5.4;
        LHV_in = 18700.0;//18384;//  // 1MJ/kg = 429.923 BTU/lbm
		setOption("switchGasTblOpt", "GASEQL2");
		 //setOption("switchGasTblOpt", "GASTAB");
		
        // HERE IS WHERE THE INDP Variable is set.		
        Independent ind_Wfuel { varName = "Wfuel";}// autoSetup = TRUE;}

    }
    // ------------------------------
    // Burner BrnPri
    // ------------------------------
    Element Burner BrnPri
    {
        dPqP_dmd     = 0.04;
        effBase      = 1.;//0.99;
        setOption("switchBurn", "WFUEL");
		//TtCombOut = 3500;
		//Independent ind_Wfuel { varName = "Wfuel";}
		//Wfuel = 3.5;//3.22371;
		Fl_O.MNdes = 0.1;

    }
    // ------------------------------
    // Turbine TrbH
    // ------------------------------
    Element Turbine TrbH
    {
		// PRbase       = 4.984;
		// effDes       = 0.92;
		// #include "hptE3.map";
		// a_effAud = 0;
		
		
		PRbase       = 6;
		effDes       = 0.8835;
		#include "hptCFM56.map";
		a_effAud = 0.0;		
		setOption("switchAud", "AUDIT");
		
		FlowStation F041;
		// Turbine Cooling and Leakage Air (TCLA) stream 1 
		// (introduced at turbine inlet)
		InterStageBleedInPort TCLA_NC { 
			Pfract  = 1.0;
			diaPump = 0.0;
		} 
		
		// Turbine Cooling and Leakage Air (TCLA) stream 2 
		// (introduced at turbine exit)
		InterStageBleedInPort TCLA_CH { 
			Pfract  = 0.0;
			diaPump = 0.0;
		}
		
		// Subelement to calculate the amount of cooling flow required to cool the turbine	
		Subelement CoolIt Cool 
		{
			nStages = 1;
			bldNameFirstRow = "TCLA_NC";
			bldName = "TCLA_CH";
			TvaneDes[0] = 2200;
			TbladeDes[0] = 2000;
			s_BldFirstRow = 1.4;  
			s_Bld = 1.1;  
		}   

		void postexecute() 
		{ 
			F041.copyFlow("Fl_I");
			F041.add("TCLA_NC");
			if ( switchDes == "DESIGN" ) 
			{
				Cool.run();
			}
		}
	}
	// ------------------------------
	// End of Cust Bleed interstage
	// ------------------------------
	Element FlowEnd FeOBA
	{
	}
	// ------------------------------
	// End of Cust Bleed B
	// ------------------------------
	Element FlowEnd FeOBB
	{
	}	
	// ------------------------------
	// Shaft ShH
	// ------------------------------
	Element Shaft ShH
	{
		ShaftInputPort CmpH, TrbH;
		ShaftOutputPort	PowerExtract;
		Nmech = 9000.0;
		inertia = 10.0;
	}
	// ------------------------------
	// Horsepower Extraction
	// ------------------------------
	Element CustomerPowerExtract CpwrH{
		ShaftInputPort PowerRecieve;
		setOption("switchPwr", "OFF");
		}
	
	// ------------------------------	
	// Link Ports
	// ------------------------------
	// Fluid Ports
	linkPorts( "CmpH.Fl_O"        , "B030.Fl_I"       , "F030"  );
	linkPorts( "B030.Fl_O"        , "D031.Fl_I"       , "F031"  );
	linkPorts( "D031.Fl_O"        , "BrnPri.Fl_I"     , "F035"  );
	linkPorts( "BrnPri.Fl_O"      , "TrbH.Fl_I"       , "F040"  );
	// Bleed Connections
	linkPorts( "CmpH.Fl_B027"     , "FeOBA.Fl_I"      , "F027_OB"   );
	linkPorts( "B030.TCLA_NC"     , "TrbH.TCLA_NC"    , "TrbH_NC"   );
	linkPorts( "B030.TCLA_CH"     , "TrbH.TCLA_CH"    , "TrbH_CH"   );
	linkPorts( "B030.Fl_B030B"    , "FeOBB.Fl_I"      , "FB030B_OB" );
	// Fuel Linkage
	linkPorts( "FusPri.Fu_O"      , "BrnPri.Fu_I"      , "FuPri" );
	// Shaft Connections
	linkPorts( "CmpH.Sh_O"        , "ShH.CmpH"        , "MeCmpH"    );
	linkPorts( "TrbH.Sh_O"        , "ShH.TrbH"        , "MeTrbH"    );
	
	linkPorts( "ShH.PowerExtract" , "CpwrH.PowerRecieve", "power"     );
	
	promotePort( "CmpH.Fl_I", "Fl_I");
	promotePort( "TrbH.Fl_O", "Fl_O");
	// Promote the interstage bleed out ports so that they can be linked to the
	//  LPT in the main model file
	promotePort( "CmpH.Fl_B_CH", "LPT_CH");
	promotePort( "CmpH.Fl_B_NC", "LPT_NC");
	
	
	//Turbine cooling Bleeds
	Independent ind_TCLA_NC_fracW { varName = "B030.TCLA_NC.fracW"; description = "Bleed flow"; }
	Independent ind_TCLA_CH_fracW { varName = "B030.TCLA_CH.fracW"; description = "Bleed flow"; }
	
	void variableChanged(string name, any oldval)
	{
		if (name == "switchDes")
		{
			if (switchDes == "DESIGN")
			{
				// in DESIGN mode, allow cycle changes such as engine flow and bypass ratio
				Core.FusPri.ind_Wfuel.autoSetup = TRUE;
				Core.ind_TCLA_CH_fracW.autoSetup = TRUE;
				Core.ind_TCLA_NC_fracW.autoSetup = TRUE;
			}
			else
			{
				
				Core.ind_TCLA_CH_fracW.autoSetup = FALSE;
				Core.ind_TCLA_NC_fracW.autoSetup = FALSE;
			}
		}
	} //end variableChanged
	


} //end Core
